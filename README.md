# ðŸ“˜ FunctionGenerator

A lightweight **FreeRTOS-based Function Generator** library for the **ESP32** (Arduino framework).  
It continuously generates configurable analog-like waveforms (sine, square, triangle, saw, DC) in a **dedicated background task**, allowing your main loop to remain free for other work.

---

## âœ¨ Features

- Runs waveform generation in its own dedicated FreeRTOS task  
- **Waveform mode**, **amplitude**, **DC offset**, and **period** can be modified dynamically at runtime or after object creation  
- **Task update period** is defined during instantiation and remains fixed for the lifetime of the object  
- Supports multiple waveform modes:
  - `DC`, `Sine`, `Square`, `Saw`, `Triangle`
- Thread-safe `getValue()` accessor using FreeRTOS critical sections  
- Designed for real-time signal synthesis and testing  
- Lightweight and easy to integrate

---
## ðŸ§© Example Usage

```cpp
#include <Arduino.h>
#include "FunctionGenerator.h"

#define FG_UPDATE_INTERVAL_SINE_MS 100
#define FG_UPDATE_INTERVAL_SQUARE_MS 1000

// #define DEBUG // uncoment to enable output of task stats
#ifdef DEBUG
void printTaskStats();
#endif

/*-------------------------------------------------------
  Example usage: Create FunctionGenerator instances
  The arguments defines the waveform update interval (ms).
--------------------------------------------------------*/
FunctionGenerator fg1(FG_UPDATE_INTERVAL_SINE_MS);
FunctionGenerator fg2(FG_UPDATE_INTERVAL_SQUARE_MS);

void setup()
{
  Serial.begin(115200);  

  // Select waveform type (DC, Sine, Square, Saw, Triangle)
  fg1.setMode(FunctionGenerator::FgMode::Sine); 
  fg1.setAmplitude(10);
  fg1.setDcOffset(0);
  fg1.setPeriod(FG_UPDATE_INTERVAL_SINE_MS * 20); 
  // For smoothly varying functions (e.g., sine, triangle, saw),
  // the update interval must be much smaller than the total period
  // to ensure the waveform appears continuous and not stepped.

  fg2.setMode(FunctionGenerator::FgMode::Square);
  fg2.setAmplitude(0.5);
  fg2.setDcOffset(0.5);
  fg2.setPeriod(FG_UPDATE_INTERVAL_SQUARE_MS * 2); 
  // For discontinuous functions (e.g., square wave), the update
  // interval can be closer to the waveform period since the value
  // changes abruptly between levels.
}

void loop()
{
  /*-----------------------------------------------------
    Retrieve the current waveform value generated by fg.
    The value changes continuously in the background,
    as the FreeRTOS task handles waveform generation.
  ------------------------------------------------------*/
  Serial.print("FG1: ");
  Serial.print(fg1.getValue(), 2);
  Serial.print(", FG2: ");
  Serial.println(fg2.getValue(), 0);
  delay(FG_UPDATE_INTERVAL_SINE_MS);

#ifdef DEBUG
  printTaskStats();
#endif
}

#ifdef DEBUG
/*-------------------------------------------------------
FreeRTOS configuration flags for runtime stats.
These enable CPU usage monitoring of each running task.
--------------------------------------------------------*/
#define configUSE_TRACE_FACILITY 1
#define configGENERATE_RUN_TIME_STATS 1

/*-------------------------------------------------------
  Debug helper function: printTaskStats()
  Prints the runtime statistics for each FreeRTOS task.
  Useful to verify that the FunctionGenerator task does
  not consume excessive CPU time.
--------------------------------------------------------*/
void printTaskStats()
{
  static uint32_t last = 0;
  if (millis() - last >= 1000)
  {
    last = millis();
    const int maxTasks = 20;
    TaskStatus_t taskStatusArray[maxTasks];
    uint32_t totalRunTime;

    // Get system state of all tasks
    UBaseType_t numTasks = uxTaskGetSystemState(taskStatusArray, maxTasks, &totalRunTime);

    Serial.printf("Task Name\tCPU %%\n");
    for (int i = 0; i < numTasks; i++)
    {
      float percent = (100.0f * taskStatusArray[i].ulRunTimeCounter) / totalRunTime;
      Serial.printf("%s\t%.2f%%\n", taskStatusArray[i].pcTaskName, percent);
    }
  }
}
#endif